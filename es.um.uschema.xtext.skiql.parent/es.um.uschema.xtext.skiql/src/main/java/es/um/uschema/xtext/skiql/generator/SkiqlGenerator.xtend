/*
 * generated by Xtext 2.23.0
 */
package es.um.uschema.xtext.skiql.generator

import java.io.File

import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import org.eclipse.emf.ecore.resource.Resource

import es.um.uschema.USchema.USchemaPackage
import es.um.uschema.USchema.USchema
import es.um.uschema.utils.EcoreModelIO

import es.um.uschema.xtext.skiql.SkiQLModel
import es.um.uschema.xtext.skiql.generator.reducer.USchemaReducer

class SkiqlGenerator extends AbstractGenerator {
	
	static final String SHOW = "SHOW|"
	static final String FILE_URI = "userprofile.xmi"

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		val skiqlModel = resource.contents.filter(SkiQLModel).head
		if (skiqlModel !== null) {
			println(generateHtml(skiqlModel))
			val html = generateHtml(skiqlModel)
			if (html !== null && html.length > 0)
				fsa.generateFile('/DEFAULT_ARTIFACT', html)
		}
	}
		
	private def generateHtml(SkiQLModel skiqlModel) 
	{
		if (skiqlModel !== null && skiqlModel.query !==null) {
			val EcoreModelIO modelLoader = new EcoreModelIO()
			modelLoader.registerPackages(USchemaPackage.eINSTANCE)
			var USchema uNoSQLSchema = modelLoader.load(USchema, new File("./inputs/" + FILE_URI).toPath)
			
			uNoSQLSchema = new USchemaReducer(skiqlModel, uNoSQLSchema).reduceModel
						
			val visjsDivName = "graph"
			val googleChartsDivName = "treemap"
			val tableDivName = "var-table"
			
			'''
				<html>
					<head>
				    	<meta charset="UTF-8">
				    	«IF uNoSQLSchema.entities.filter[e | !e.name.startsWith(SHOW)].size > 0 || uNoSQLSchema.relationships.filter[r | !r.name.startsWith(SHOW)].size > 0»
				    	«new VisJSGenerator().generateVisJS(uNoSQLSchema, visjsDivName)»
				    	«new GoogleChartGenerator().generateGoogleChart(uNoSQLSchema, googleChartsDivName)»
				    	«ENDIF»
					</head>
				  	<body>
				    	«IF uNoSQLSchema.entities.filter[e | !e.name.startsWith(SHOW)].size > 0 || uNoSQLSchema.relationships.filter[r | !r.name.startsWith(SHOW)].size > 0»
				    	<div id="«visjsDivName»"></div>
				    	<div id="«googleChartsDivName»"></div>
				    	«ELSE»
				    	<div id="no-results"><h2>No results</h2></div>
				    	«ENDIF»
				  </body>
				</html>
				$$-$$
				<table id="var-table" class="display">
					<thead>
					    <tr>
					        <th>Entity</th>
					        <th>Variation</th>
					        <th>Count</th>
					        <th>Attributes</th>
					    </tr>
					</thead>
					<tbody>
						«new TableGenerator().generateTable(uNoSQLSchema, tableDivName)»
					</tbody>
					<tfoot>
					    <tr>
					        <th>Entity</th>
					        <th>Variation</th>
					        <th>Count</th>
					        <th>Attributes</th>
					    </tr>
					</tfoot>
				</table>
			'''
		}
	}
	
}
